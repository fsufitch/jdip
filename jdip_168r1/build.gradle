apply plugin: 'java'
apply plugin: 'application'



repositories {

    jcenter()
}


dependencies {

    // Add all the jar dependencies from the lib folder.
    compile fileTree(dir: 'lib', include: ['*.jar'])

    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.8'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.9.8'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.8'

    testImplementation(
            'org.junit.jupiter:junit-jupiter-api:5.3.2'
    )
    testRuntimeOnly(
            'org.junit.jupiter:junit-jupiter-engine:5.3.2'
    )
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.3.2'
    testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.0'


}

test {
    useJUnitPlatform()
}

file('resource/variants').eachDir { dir ->
    def taskName = "${dir.name}Variants"
    def task = tasks.create(name: taskName, type: Zip) {
        baseName = taskName
        from fileTree(dir)
        destinationDir file("build/tmp/variants")
        group='variants'
    }
    test.dependsOn(task)
    run.dependsOn(task)

}
file('resource/symbols').eachDir { dir ->
    def taskName = "${dir.name}Symbols"
    def task = tasks.create(name: taskName, type: Zip) {
        baseName = taskName
        from fileTree(dir)
        destinationDir file("build/tmp/variants")
        group='variants'

    }
    test.dependsOn(task)
    run.dependsOn(task)
}


mainClassName = 'info.jdip.gui.splash'

distributions {
    main {
        baseName = "jdip"
        contents{
            tasks.each {
                task ->
                    if (task.group == 'variants') {
                        from(task){
                            into 'variants'
                        }
                    }
            }

            from('.') {
                include 'README.TXT'
                include 'LICENSE.TXT'
                include 'HISTORY.TXT'
                include 'CHANGELOG.TXT'
            }
        }
    }
}

run{
    workingDir("build/tmp")
}



